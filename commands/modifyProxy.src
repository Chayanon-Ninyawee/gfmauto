modifyProxy = {}
modifyProxy.cmd = "modifyProxy"
modifyProxy.alias = "modproxy"
modifyProxy.args = "(-a)"
modifyProxy.desc = "Modify proxy server in the list"

modifyProxy.run = function(args)
    is_show_hidden = false

    if args.len != 0 then
        if args[0] == "-a" then
            is_show_hidden = true
        else
            exit("Invalid flag!")
        end if
    end if
    
    while true

    proxy_server_list = get_proxy_server_list

    print("[-1]: Move proxy server")
    if not proxy_server_list.len == 0 then
        for i in range(0, proxy_server_list.len-1)
            proxy_server = proxy_server_list[i]
            print("["+i+"]: "+modifyProxy.get_proxy_server_info_string(proxy_server, is_show_hidden))
        end for
    end if
    print("["+proxy_server_list.len+"]: Add new proxy server")

    selected_index = user_input("Select proxy server: ").to_int
    if typeof(selected_index) == "string" then; print("Invalid index!"); continue; end if
    if selected_index < -1 or selected_index > proxy_server_list.len then; print("Invalid index!"); continue; end if

    if selected_index == proxy_server_list.len then
        print("[0]: Add NPC")
        print("[1]: Add Server")

        selected_index = user_input("Select option: ").to_int
        if typeof(selected_index) == "string" then; print("Invalid index!"); continue; end if
        if selected_index < 0 or selected_index > 1 then; print("Invalid index!"); continue; end if

        if selected_index == 0 then
            server = {}
            server.ip = user_input("NPC IP: ")
            server.lan_ip = user_input("NPC LAN IP: ")
            server.ssh_port = null
            server.password = null
            server.key = null

            proxy_server_list.push(server)
            set_proxy_server_list(proxy_server_list)
            continue
        else if selected_index == 1 then
            server = {}
            server.ip = user_input("Proxy server IP: ")
            server.lan_ip = null
            server.ssh_port = user_input("Proxy server SSH port: ")
            server.password = user_input("Proxy server password: ", not is_show_hidden)
            server.key = user_input("Proxy server AES key (if not setup yet just press enter): ", not is_show_hidden)

            if server.ip == "" then; print("Invalid IP!"); continue; end if
            if server.ssh_port == "" then; print("Invalid SSH port!"); continue; end if
            if server.password == "" then; print("Invalid password!"); continue; end if
            if server.key == "" then
                server.key = null
            else if not server.key.len == 16 then
                print("Key must be exactly 16 characters!"); continue
            end if

            proxy_server_list.push(server)
            set_proxy_server_list(proxy_server_list)
            continue
        end if
    else if selected_index == -1 then
        proxy_server_list_copy = proxy_server_list[:]
        new_proxy_server_list = []
        arrangement = user_input("Arrangement (Example: 0,3,2,1): ").to_int

        for index in arrangement.split(",")
            index = index.to_int
            if typeof(index) == "string" then; print("Invalid arrangement!"); continue; end if
            if index < 0 or index >= proxy_server_list.len then; print("Invalid arrangement!"); continue; end if

            new_proxy_server_list.push(proxy_server_list[index])
            proxy_server_list_copy[index] = null
        end for

        for proxy_server in proxy_server_list_copy
            if proxy_server == null then continue
            new_proxy_server_list.push(proxy_server)
        end for

        set_proxy_server_list(new_proxy_server_list)
        continue
    else
        proxy_server = proxy_server_list[selected_index]
        print("Selected proxy server: "+modifyProxy.get_proxy_server_info_string(proxy_server, is_show_hidden))
        print("[0]: Modify")
        print("[1]: Delete")
        option = user_input("Select option: ").to_int
        if typeof(option) == "string" then; print("Invalid index!"); continue; end if
        if option < 0 or option > 1 then; print("Invalid index!"); continue; end if

        if option == 0 then
            if proxy_server.ssh_port == null or proxy_server.password == null then
                server = {}
                server.ip = user_input("Proxy server IP (press enter to skip): ")
                server.lan_ip = user_input("Proxy server LAN IP (press enter to skip): ")
                server.ssh_port = null
                server.password = null
                server.key = null

                if server.ip == "" then server.ip = proxy_server.ip
                if server.lan_ip == "" then server.lan_ip = proxy_server.lan_ip

                proxy_server_list[selected_index] = server
                set_proxy_server_list(proxy_server_list)
            else
                server = {}
                server.ip = user_input("Proxy server IP (press enter to skip): ")
                server.lan_ip = null
                server.ssh_port = user_input("Proxy server SSH port (press enter to skip): ")
                server.password = user_input("Proxy server password (press enter to skip)", not is_show_hidden)
                server.key = user_input("Proxy server AES key (press enter to skip or type 'clear' to clear the key): ", not is_show_hidden)

                if server.ip == "" then server.ip = proxy_server.ip
                if server.ssh_port == "" then server.ssh_port = proxy_server.ssh_port
                if server.password == "" then server.password = proxy_server.password
                if server.key == "" then; server.key = proxy_server.key
                else if server.key == "clear" then; server.key = null
                else if not server.key.len == 16 then; print("Key must be exactly 16 characters!"); continue; end if

                proxy_server_list[selected_index] = server
                set_proxy_server_list(proxy_server_list)
            end if
            print("Modified proxy server ["+selected_index+"]")
        else if option == 1 then
            proxy_server_list.remove(selected_index)
            set_proxy_server_list(proxy_server_list)
            print("Deleted proxy server ["+selected_index+"]")
        end if
    end if

    end while

end function

modifyProxy.get_proxy_server_info_string = function(proxy_server, is_show_hidden)
    proxy_server_info_string = "* "
    if not proxy_server.key == null or not proxy_server.lan_ip == null then proxy_server_info_string = "  "

    proxy_server_info_string = proxy_server_info_string+proxy_server.ip
    if not proxy_server.ssh_port == null then; proxy_server_info_string = proxy_server_info_string+":"+proxy_server.ssh_port
    else if not proxy_server.lan_ip == null then; proxy_server_info_string = proxy_server_info_string+" "+proxy_server.lan_ip; end if
    
    if is_show_hidden then
        proxy_server_info_string = proxy_server_info_string+" "+proxy_server.password
        if not proxy_server.key == null then proxy_server_info_string = proxy_server_info_string+" "+proxy_server.key
    end if
    return proxy_server_info_string
end function